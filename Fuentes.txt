Código del programa en NetLogo

turtles-own [
  cubrebocas?       ;Indica si el agente utiliza cubrebocas
  vacuna?           ;Indica Si el agente está vacunado
  distanciamiento?   ;Indica si el agente pone en práctica el distanciamiento social
  inicio-enfermedad  ;Tick en el que se contagió
]

patches-own [
  residuo ;Residuo de estornudos que queda en el patch
]

globals [
  Emax                   ;tiempo en el que se alcanza el pico de infectados
  max-infected           ;máximo número de infectados al mismo tiempo
  poblacion-inicial      ;número inicial de agentes
  muertos                ;número de agentes muertos
  total-infected         ;total de agentes infectados
  suceptibles-iniciales  ;agentes sanos iniciales
  contagios-contacto     ;agentes que se contagiaron por contacto
  contagios-estornudo    ;agentes que se contagiaron por estornudo
  contagios              ;agentes que se contagiaron
]
;Inicializa la variable inicio-enfermedad en -1
;para aquellos agentes que no se han contagiado.
;
;Inicializa las variables cubrebocas? vacuna? y distanciamiento?
;de acuerdo al las variables p-cubrebocas, p-vacuna y p-distanciamiento
;respectivamente.
;
;Inicializa las variables poblacion-inicial, max-infected, suceptibles-iniciales y Emax
to setup
  clear-all
  reset-ticks
  create-turtles (poblacion / 100) * (count patches) [
    setxy random-xcor random-ycor
    set shape "person"
    set size 0.9
    set inicio-enfermedad -1
    setup-infectados
    set distanciamiento? random 100 < p-distanciamiento
    set cubrebocas? random 100 < p-cubrebocas
    set vacuna? random 100 < p-vacuna
  ]
  set poblacion-inicial (count turtles)
  set max-infected count turtles with [color = red]
  set suceptibles-iniciales count turtles with [color = blue]
  set Emax 0
  setup-patches
end

;Infecta a un pocentaje de la población correspondiente
;al valor de S0.
to setup-infectados
  if-else (random 100 < S0)
    [
      set color blue
    ]
    [
      set color red
      set total-infected total-infected + 1
      set inicio-enfermedad ticks
    ]
end

;Colorea el patch de acuerdo a la cantidad de residuo en él
to setup-patches
  ask patches [
    recolor-patch
  ]
end

;Colorea el patch de acuerdo a la cantidad de residuo en él
to recolor-patch
  set pcolor scale-color yellow residuo 0.005 1
end

to go
  if not any? turtles with [color = red] [
    stop
  ]
  move
  estornudar
  difundir
  infect-sneeze
  infect
  recover-or-die
  tick
  if (max-infected < count turtles with [color = red]) [
    set max-infected count turtles with [color = red]
    set Emax ticks
  ]
  set contagios contagios-estornudo + contagios-contacto
end

;Gira las tortugas en un rango de -60 a 60 grados y
;avanza 1
to move
  ask turtles [
    if distanciamiento? = false [
      rt random (-60 + random 120)
      forward 1
    ]
  ]
end

;Infecta con cierta probabilidad a las tortugas que
;se encuentren en una vecindad de Moore.
;Si el agente utiliza cubrebocas, reduce la probabilidad
;de contagio al 25%.
;Si el agente está vacunado reduce la probabilidad de
;contagio al 10%.
to infect
  ask turtles with [color = red] [
    ask turtles-on neighbors [
      if color = blue [
        let p-i ifelse-value cubrebocas?
        [p-infeccion * 0.25]
        [p-infeccion]
        set p-i ifelse-value vacuna?
        [p-i * 0.1]
        [p-i]
        if (random-float 1 < p-i)
        [
          set color red
          set contagios-contacto contagios-contacto + 1
          set total-infected total-infected + 1
          set inicio-enfermedad ticks
        ]
      ]
    ]
  ]
end

;Verifica, para cada agente, si ya pasaron tantos
;ticks como dura la enfermedad desde que se infectó.
;Si el agente está vacunado, se reduce su probabilidad
;de muerte al 10%.
;De acuerdo a la probabilidad de muerte, el agente muere
;o se recupera.
to recover-or-die
  ask turtles with [color = red] [
    if ((ticks - inicio-enfermedad) >= duracion)
    [
      set color green
      let p-m ifelse-value vacuna?
      [p-muerte * 0.1]
      [p-muerte]
      if (random-float 1 < p-m) [
        set muertos muertos + 1
        die
      ]
    ]
  ]
end

;Aumenta el residuo del patch en el que se encuentra el agente
;que no utiliza cubrebocas.
to estornudar
  ask turtles with [color = red and not cubrebocas?] [
    if (random-float 1 < p-sneeze) [
      set residuo residuo + 1
    ]
  ]
end

;Infecta, con cierta probabilidad, a los agentes (sin cubrebocas) que
;se encuentren en un patch con residuo dependiendo de cuánto residuo haya.
;Si el agente está vacunado, esta probabilidad se reduce al 10%
to infect-sneeze
  ask turtles with [color = blue and not cubrebocas?] [
    let res ifelse-value vacuna?
    [residuo * 0.1]
    [residuo]
    if (random-float 1 < res) [
      set color red
      set contagios-estornudo contagios-estornudo + 1
      set total-infected total-infected + 1
      set inicio-enfermedad ticks
    ]
  ]
end

;Difunde el 80% residuo en los patches vecinos y lo evapora
to difundir
  diffuse residuo (80 / 100)
  ask patches [
    set residuo residuo * (100 - 50) / 100
    recolor-patch
  ]
end